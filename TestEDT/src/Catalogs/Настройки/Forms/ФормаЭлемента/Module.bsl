#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ТипНастройки.СписокВыбора.Очистить();
	Элементы.ТипНастройки.СписокВыбора.Добавить(0, "Простой");
	Элементы.ТипНастройки.СписокВыбора.Добавить(1, "Список");
	Элементы.ТипНастройки.СписокВыбора.Добавить(2, "Соответствие");
	Элементы.ТипНастройки.СписокВыбора.Добавить(3, "Пользователи (сериализованно)");

	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ОбновитьЗначениеИзСериализуемойСтрокиНаФорме(ТекущийОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если Не ЗначениеЗаполнено(Объект.Имя) Тогда
		УстановитьGUID();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключитьОбработчикОжидания("ОбновитьНадписьНаФорме", 0.1, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ЗначениеСериализацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.ТипНастройки > 2 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли; 
	
	Если Не СтандартнаяОбработка Тогда
		
		ПараметрыОтбор = ПолучитьПараметрыОткрытияФормы();
		ОписаниеОповещенияЗакрытие = Новый ОписаниеОповещения("ОбработкаОписаниеОповещенияЗакрытия", ЭтаФорма);
		ОткрытьФорму("ОбщаяФорма.СписокЗначенийОтбора",ПараметрыОтбор , , Новый УникальныйИдентификатор,,,ОписаниеОповещенияЗакрытие);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОписаниеОповещенияЗакрытия(Результат, ДополнительныеПараметры) Экспорт

	Если ЗначениеЗаполнено(Результат) Тогда
		ОбработкаОписаниеОповещенияЗакрытияНаСервере(Результат, ДополнительныеПараметры);
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаОписаниеОповещенияЗакрытияНаСервере(Результат, ДополнительныеПараметры)
	
	Объект.ЗначениеСериализация = ЗначениеВСтрокуВнутр(Результат);
	
	ОбновитьЗначениеИзСериализуемойСтрокиНаФорме(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеСписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеСписокПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ТипНастройкиПриИзменении(Элемент)
	
	ОбновитьНадписьНаФорме();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОграничитьТипПриВыборе(Элемент, ДанныеВыбора, СтандартнаяОбработка);	
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОграничитьТипПриВыборе(Элемент, ДанныеВыбора, СтандартнаяОбработка);	
	
КонецПроцедуры

&НаКлиенте
Процедура ОграничитьТипПриВыборе(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Родитель = ПредопределенноеЗначение("Справочник.Настройки.Валюты") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("СправочникСсылка.Валюты"));
		НашеОписание = Новый ОписаниеТипов(Массив);
		Элемент.ОграничениеТипа = НашеОписание;
		
	ИначеЕсли Объект.Родитель = ПредопределенноеЗначение("Справочник.Настройки.Портфели") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("СправочникСсылка.Портфели"));
		НашеОписание = Новый ОписаниеТипов(Массив);
		Элемент.ОграничениеТипа = НашеОписание;
		
	ИначеЕсли Объект.Родитель = ПредопределенноеЗначение("Справочник.Настройки.Контрагенты") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("СправочникСсылка.Контрагенты"));
		НашеОписание = Новый ОписаниеТипов(Массив);
		Элемент.ОграничениеТипа = НашеОписание;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СгенерироватьGUID(Команда)
	
	УстановитьGUID();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьGUID()

	Наименование = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими(" ,()""'!",Объект.Наименование,"");
	Объект.Имя = "ГПН_" + ЛЕВ(Наименование,64) + "_" + СтрЗаменить(Новый УникальныйИдентификатор(), "-", "");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьНадписьНаФорме()
	
	ПростоеЗначение = (Объект.ТипНастройки = 0);
	ЗначениеСписком = (Объект.ТипНастройки = 1);
	ЗначениеСоответствие = (Объект.ТипНастройки =2);

	ЗначениеСериализованно = (Объект.ТипНастройки > 2);
	
	Если ПростоеЗначение Тогда
		
		ЗначениеСписок.Очистить();
		Объект.Список.Очистить();
		Объект.ЗначениеСериализация = "";
		
	ИначеЕсли ЗначениеСписком Тогда 
		
		ЗначениеСписок.Очистить();
		
		Объект.Значение = Неопределено;
		Объект.ЗначениеСериализация = "";
		
	ИначеЕсли ЗначениеСоответствие Тогда 
		
		ЗначениеСписок.Очистить();
		
		Объект.Значение = Неопределено;
		Объект.ЗначениеСериализация = "";
		
	ИначеЕсли ЗначениеСериализованно Тогда
		
		Объект.Значение = Неопределено;
		Объект.Список.Очистить();
		
	КонецЕсли; 
	
	Элементы.ГруппаЗначение.Доступность = Не ЗначениеСериализованно И Не ЗначениеСписком И Не ЗначениеСоответствие;
	Элементы.ГруппаЗначениеСериализация.Видимость = ЗначениеСериализованно;
	Элементы.ГруппаЗначениеСписком.Видимость = ЗначениеСписком Или ЗначениеСоответствие;
	Элементы.Список.ПодчиненныеЭлементы.СписокСоответствие.Видимость = ЗначениеСоответствие;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЗначениеИзСериализуемойСтрокиНаФорме(ТекущийОбъект)
	
	Если ТекущийОбъект.ТипНастройки > 2 Тогда
		
		ЗначениеИзСериализации = УА_ОбщегоНазначенияСервер.ЗначениеИзСериализованнойСтроки(ТекущийОбъект.ЗначениеСериализация);
		
		Если ТипЗнч(ЗначениеИзСериализации) = Тип("СписокЗначений") Тогда
			ЗначениеСписок.ЗагрузитьЗначения(ЗначениеИзСериализации.ВыгрузитьЗначения());
		КонецЕсли; 
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметрыОткрытияФормы()
	
	ПараметрыОткрытия =  Новый Структура;
	
	СписокТипов = Новый Массив;
	
	//Пользователи
	Если Объект.ТипНастройки = 3 Тогда
		СписокТипов.Добавить(Тип("СправочникСсылка.Пользователи"));
	//Другая настройка	
	Иначе 
		
	КонецЕсли; 
	
	ПараметрыОткрытия.Вставить("ТипПоля", Новый ОписаниеТипов(СписокТипов));
	                                                       
	ПараметрыОткрытия.Вставить("Значение", УА_ОбщегоНазначенияСервер.ЗначениеИзСериализованнойСтроки(Объект.ЗначениеСериализация));
	
	ПараметрыОткрытия.Вставить("СписокПараметров", Новый Структура("ВыборГруппИЭлементов", ИспользованиеГруппИЭлементов.Элементы));
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

#КонецОбласти
