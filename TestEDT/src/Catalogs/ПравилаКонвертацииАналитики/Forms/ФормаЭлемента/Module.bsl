
#Область СОБЫТИЯ_ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры


#КонецОбласти

#Область СОБЫТИЯ_ЭЛЕМЕНТОВ_ФОРМЫ

&НаКлиенте
Процедура ПроизвольныйАлгоритмПриИзменении(Элемент)

	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ВСПОМОГАТЕЛЬНЫЕ_ПРОЦЕДУРЫ_ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Элементы.РеквизитИсточника.СписокВыбора.ЗагрузитьЗначения(ПолучитьРеквизитыИсточника(Объект.Источник));
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ПроизвольныйАлгоритм = Объект.ПроизвольныйАлгоритм;
	Элементы.РеквизитИсточника.Видимость 	= Не ПроизвольныйАлгоритм;		
	Элементы.ТекстАлгоритма.Видимость 		= ПроизвольныйАлгоритм;		
	Элементы.ГруппаПодсказка.Видимость 		= ПроизвольныйАлгоритм;		
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитИсточникаНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	//СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсточникПриИзменении(Элемент)
	
	Элементы.РеквизитИсточника.СписокВыбора.ЗагрузитьЗначения(ПолучитьРеквизитыИсточника(Объект.Источник));
	
КонецПроцедуры

&НаСервере
Функция ПолучитьРеквизитыИсточника(Источник)
	
	Если Не ЗначениеЗаполнено(Источник) Тогда 	
		Возврат Новый Массив;
	КонецЕсли;
	
	ТаблицаИменРеквизитов = Новый ТаблицаЗначений;
	ТаблицаИменРеквизитов.Колонки.Добавить("ИмяРеквизита");
	
	Для Каждого ТипИсточника Из Источник.ТипЗначения.Типы() Цикл 
		МетаданныеТипаИсточника = Метаданные.НайтиПоТипу(ТипИсточника);
		Если ОбщегоНазначения.ЭтоОбъектСсылочногоТипа(МетаданныеТипаИсточника) Тогда 
			Для Каждого Реквизит Из МетаданныеТипаИсточника.Реквизиты Цикл
				Строка = ТаблицаИменРеквизитов.Добавить();
				Строка.ИмяРеквизита = Реквизит.Имя;
			КонецЦикла;
			
			Для Каждого СтандартныйРеквизит Из МетаданныеТипаИсточника.СтандартныеРеквизиты Цикл
				Строка = ТаблицаИменРеквизитов.Добавить();
				Строка.ИмяРеквизита = СтандартныйРеквизит.Имя;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;	
	
	ТаблицаИменРеквизитов.Свернуть("ИмяРеквизита");
	ТаблицаИменРеквизитов.Сортировать("ИмяРеквизита");
	
	Если ТаблицаИменРеквизитов.Количество() > 0 Тогда 
		Возврат ТаблицаИменРеквизитов.ВыгрузитьКолонку("ИмяРеквизита");
	Иначе
		Возврат Новый Массив;
	КонецЕсли;	
	
КонецФункции

#КонецОбласти
