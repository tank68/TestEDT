
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Элементы.ФормаЛицевыеСчетаСОшибкамиГенерацииНомера.Пометка = Ложь;
КонецПроцедуры
	
#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ЛицевыеСчетаСОшибкамиГенерацииНомера(Команда)
	
	ПереключитьВидимостьУдаленныхОбъектов(Не Элементы.ФормаЛицевыеСчетаСОшибкамиГенерацииНомера.Пометка);
		
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНомераЛицевыхСчетов(Команда)
	
	МассивЛицевыхСчетов = ПолучитьМассивЛицевыхСчетов();
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("МассивЛицевыхСчетов", МассивЛицевыхСчетов);
	
	ОткрытьФорму("Справочник.уа_ЛицевыеСчетаЕПС.Форма.ФормаОбновленияКода", ПараметрыФормы, ЭтаФорма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПереключитьВидимостьУдаленныхОбъектов(Видимость)
	
	Элементы.ФормаЛицевыеСчетаСОшибкамиГенерацииНомера.Пометка = Видимость;	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "ОшибкаКодирования", Истина, , , Видимость);
			
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивЛицевыхСчетов()
	
	//NFO-1316
	Схема		= Элементы.Список.ПолучитьИсполняемуюСхемуКомпоновкиДанных();
	Настройки	= Элементы.Список.ПолучитьИсполняемыеНастройкиКомпоновкиДанных();
	
	КомпоновщикМакета	= Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки	= КомпоновщикМакета.Выполнить(Схема, Настройки,,,Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки	= Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);  
	
	ПроцессорВывода	= Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	Результат	= Новый ТаблицаЗначений;
	
	ПроцессорВывода.УстановитьОбъект(Результат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат Результат.ВыгрузитьКолонку("Ссылка");
		
КонецФункции

#КонецОбласти
