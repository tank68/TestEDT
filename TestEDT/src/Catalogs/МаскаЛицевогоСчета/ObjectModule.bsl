#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда  

#Область ОБРАБОТЧИКИ_СОБЫТИЙ 

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Или ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;

	Если Не Справочники.МаскаЛицевогоСчета.СинтаксическийКонтрольШаблонаПройден(Шаблон) Тогда 
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.КлючДанных = Ссылка;
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Поле = "Шаблон";
		Сообщение.Текст = "Синтаксический контроль шаблона не пройден";
		Сообщение.Сообщить(); 

		Отказ = Истина;	
	КонецЕсли;
	
	Если Не ЭтоНовый() Тогда 
		ПроверкаВозможностиИспользованияВидовАналитики(Отказ);
	КонецЕсли;
	
	ПредставлениеЛицевогоСчета = СтрЗаменить(ПредставлениеЛицевогоСчета, " ", "");
	
	ВидыАналитик.Свернуть("ВидАналитики");
	
КонецПроцедуры

#КонецОбласти

#Область ВСПОМОГАТЕЛЬНЫЕ_ПРОЦЕДУРЫ_ФУНКЦИИ 

Процедура ПроверкаВозможностиИспользованияВидовАналитики(Отказ)
	
	ТаблицаНастроекИспользующихМаску = ТаблицаНастроекИспользующихМаску();
	
	Для Каждого СтрокаВидовАналитикиМаски из ВидыАналитик Цикл
		ВидАналитикиМаски = СтрокаВидовАналитикиМаски.ВидАналитики;
		
		Для Каждого СтрокаАналитики Из ТаблицаНастроекИспользующихМаску Цикл
			Если  СтрокаАналитики.Аналитика1 <> ВидАналитикиМаски 
				И СтрокаАналитики.Аналитика2 <> ВидАналитикиМаски 
				И СтрокаАналитики.Аналитика3 <> ВидАналитикиМаски 
				И СтрокаАналитики.Аналитика4 <> ВидАналитикиМаски 
				И СтрокаАналитики.Аналитика5 <> ВидАналитикиМаски Тогда 
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.КлючДанных = Ссылка;
				Сообщение.ПутьКДанным = "Объект";
				Сообщение.Поле = "ВидыАналитики";
				Сообщение.Текст = "Невозможно использовать вид аналитики <"+ ВидАналитикиМаски +">, т.к. данная маска используется в счетах, где нет такого вида аналитики";
				Сообщение.Сообщить(); 
				
				Отказ = Истина;
				
			КонецЕсли;	
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры

Функция ТаблицаНастроекИспользующихМаску()
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	НастройкаАналитикиЛицевыхСчетовЕПС.Аналитика1,
		|	НастройкаАналитикиЛицевыхСчетовЕПС.Аналитика2,
		|	НастройкаАналитикиЛицевыхСчетовЕПС.Аналитика3,
		|	НастройкаАналитикиЛицевыхСчетовЕПС.Аналитика4,
		|	НастройкаАналитикиЛицевыхСчетовЕПС.Аналитика5
		|ИЗ
		|	РегистрСведений.НастройкаАналитикиЛицевыхСчетовЕПС КАК НастройкаАналитикиЛицевыхСчетовЕПС
		|ГДЕ
		|	НастройкаАналитикиЛицевыхСчетовЕПС.МаскаЛицевогоСчета = &МаскаЛицевогоСчета";
	Запрос.УстановитьПараметр("МаскаЛицевогоСчета", Ссылка);	
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти


#КонецЕсли