#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Объект.Ссылка.Пустая() Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		УникальныйИдентификаторСсылки = Строка(Объект.Ссылка.УникальныйИдентификатор());
		
		ПарольБезопасноеХранилище = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(УникальныйИдентификаторСсылки, "Пароль");
		Пароль = ?(ЗначениеЗаполнено(ПарольБезопасноеХранилище), ЭтотОбъект.УникальныйИдентификатор, "");
		
		УстановитьПривилегированныйРежим(Ложь);
		
	КонецЕсли; 
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ИспользоватьСтрокуСоединения Тогда
		
		ТекущийОбъект.Сервер = "";
		ТекущийОбъект.ИмяПользователя = "";
		ТекущийОбъект.БазаДанных = "";
		ТекущийОбъект.ДрайверПодключения = "";
		
		Пароль = "";
		ПарольИзменен = Истина;
		
	Иначе 
		
		ТекущийОбъект.СтрокаСоединения = "";
		
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПарольИзменен Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		
		УникальныйИдентификаторСсылки = Строка(ТекущийОбъект.Ссылка.УникальныйИдентификатор());
		
		ОбщегоНазначения.ЗаписатьДанныеВБезопасноеХранилище(УникальныйИдентификаторСсылки, Пароль, "Пароль");
		
		УстановитьПривилегированныйРежим(Ложь);
		
		ПарольИзменен = Ложь;
		
	КонецЕсли; 
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменен = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПарольИзменен = Истина;
    Пароль = Текст;

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьСтрокуПодключенияПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ИспользоватьСтрокуПодключения = Объект.ИспользоватьСтрокуСоединения;
	
	Элементы.ГруппаДанныеСоединения.Видимость = Не ИспользоватьСтрокуПодключения;
	Элементы.ГруппаСтрокаСоединения.Видимость = ИспользоватьСтрокуПодключения;
	
КонецПроцедуры

&НаКлиенте
Процедура АутентификацияОСПриИзменении(Элемент)
	//Объект.АутентификацияСтандартная = Не Объект.АутентификацияОС;
КонецПроцедуры

&НаКлиенте
Процедура АутентификацияСтандартнаяПриИзменении(Элемент)
	//Объект.АутентификацияОС = Не Объект.АутентификацияСтандартная;
КонецПроцедуры

#КонецОбласти
