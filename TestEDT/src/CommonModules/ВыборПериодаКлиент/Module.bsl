////////////////////////////////////////////////////////////////////////////////
// Функции и процедуры обеспечения выбора периода.
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Процедура для обработки события при изменении вида периода 
//
// Параметры:
//  Элемент - элемент формы, из которого вызвана процедура
//  ВидПериода - значение вида периода
//  НачалоПериода - начало периода
//  КонецПериода - конец периода
//  Период - период
//
// Возвращаемые значения:
//  в зависимости от вида периода получает начало и конец периода, а так же представление периода
//
Процедура ВидПериодаПриИзменении(Элемент, Знач ВидПериода, НачалоПериода, КонецПериода, Период) Экспорт
	
	Если ВидПериода <> ПредопределенноеЗначение("Перечисление.ДоступныеПериодыОтчета.ПроизвольныйПериод") Тогда
		Если ЗначениеЗаполнено(НачалоПериода) Тогда
			НачалоПериода = ВыборПериодаКлиентСервер.НачалоПериодаОтчета(ВидПериода, НачалоПериода);
			КонецПериода  = ВыборПериодаКлиентСервер.КонецПериодаОтчета(ВидПериода, НачалоПериода);
		Иначе
			НачалоПериода = Неопределено;
			КонецПериода  = Неопределено;
		КонецЕсли;
		
		Список = ВыборПериодаКлиентСервер.ПолучитьСписокПериодов(НачалоПериода, ВидПериода);
		ЭлементСписка = Список.НайтиПоЗначению(НачалоПериода);
		Если ЭлементСписка <> Неопределено тогда
			Период = ЭлементСписка.Представление;
		Иначе
			Период = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура для обработки события при изменении периода 
//
// Параметры:
//  Элемент - элемент формы, из которого вызвана процедура
//  Период - значение периода
//  НачалоПериода - начало периода
//  КонецПериода - конец периода
//
// Возвращаемые значения:
//  если период е выбран, то начало и конец периода равны Неопределено
//
Процедура ПериодПриИзменении(Элемент, Знач Период, НачалоПериода, КонецПериода) Экспорт
	
	Если ПустаяСтрока(Период) Тогда
		НачалоПериода = Неопределено;
		КонецПериода  = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

// Процедура для обработки события начала выбора периода из списка 
//
// Параметры:
//  Форма - форма, из которой вызвано событие
//  Элемент - элемент формы, из которого вызвана процедура
//  СтандартнаяОбработка - флаг использования стандартной обработки
//  ВидПериода - вид периода
//  Период - периода
//  НачалоПериода - начало периода
//  КонецПериода - конец периода
//
// Возвращаемые значения:
//  получает начало и конец периода, а так же представление периода
//
Процедура ПериодНачалоВыбора(Форма, Элемент, СтандартнаяОбработка, ВидПериода, Период, НачалоПериода, КонецПериода) Экспорт
	
	Если НачалоПериода = '00010101' Тогда
		НачалоПериода = ВыборПериодаКлиентСервер.НачалоПериодаОтчета(ВидПериода, ТекущаяДата());
	КонецЕсли;
	ВыбранныйПериод      = ВыбратьПериодОтчета(Форма, Элемент, СтандартнаяОбработка, ВидПериода, НачалоПериода);
	СтандартнаяОбработка = Ложь;
	Если ВыбранныйПериод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Период = ВыбранныйПериод.Представление;
	
	НачалоПериода = ВыбранныйПериод.Значение;
	КонецПериода  = ВыборПериодаКлиентСервер.КонецПериодаОтчета(ВидПериода, ВыбранныйПериод.Значение);
	
КонецПроцедуры

// Процедура для обработки события выбора периода 
//
// Параметры:
//  Элемент - элемент формы, из которого вызвана процедура
//  ВыбранноеЗначение - выбранное в элементе значение
//  СтандартнаяОбработка - флаг использования стандартной обработки
//  ВидПериода - вид периода
//  Период - периода
//  НачалоПериода - начало периода
//  КонецПериода - конец периода
//
// Возвращаемые значения:
//  получает начало и конец периода, а так же представление периода
//
Процедура ПериодОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка, ВидПериода, Период, НачалоПериода, КонецПериода) Экспорт
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Дата") Тогда
		НачалоПериода = ВыборПериодаКлиентСервер.НачалоПериодаОтчета(ВидПериода, ВыбранноеЗначение);
		КонецПериода  = ВыборПериодаКлиентСервер.КонецПериодаОтчета(ВидПериода, ВыбранноеЗначение);
		
		ВыбранноеЗначение = ВыборПериодаКлиентСервер.ПолучитьПредставлениеПериодаОтчета(
			ВидПериода, НачалоПериода, КонецПериода);
			
		Период = ВыбранноеЗначение;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

// Процедура для обработки автоподбора при вводе периода 
//
// Параметры:
//  Элемент - элемент формы, из которого вызвана процедура
//  Текст - вводимый текст
//  ДанныеВыбора - даные для выбора
//  Ожидание - время ожидания
//  СтандартнаяОбработка - флаг использования стандартной обработки
//  ВидПериода - вид периода
//  Период - периода
//  НачалоПериода - начало периода
//  КонецПериода - конец периода
//
// Возвращаемые значения:
//  вызывает процедуру по подбору данных выбора, которая получает начало и конец периода, а так же представление периода
//
Процедура ПериодАвтоПодбор(Элемент, Текст, ДанныеВыбора, Ожидание, СтандартнаяОбработка, ВидПериода, Период, НачалоПериода, КонецПериода) Экспорт
	
	ДанныеВыбора = ВыборПериодаКлиентСервер.ПодобратьПериодОтчета(ВидПериода, Текст, 
		НачалоПериода, КонецПериода);
		
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Процедура для обработки события окончания ввода текста при вводе периода 
//
// Параметры:
//  Элемент - элемент формы, из которого вызвана процедура
//  Текст - введенный текст
//  ДанныеВыбора - даные для выбора
//  СтандартнаяОбработка - флаг использования стандартной обработки
//  ВидПериода - вид периода
//  Период - периода
//  НачалоПериода - начало периода
//  КонецПериода - конец периода
//
// Возвращаемые значения:
//  вызывает процедуру по подбору периода отчета, которая на основании введенного текста 
//  получает начало и конец периода, а так же представление периода
//
Процедура ПериодОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка, ВидПериода, Период, НачалоПериода, КонецПериода) Экспорт
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеВыбора = ВыборПериодаКлиентСервер.ПодобратьПериодОтчета(ВидПериода, Текст, 
		НачалоПериода, КонецПериода);
		
	СтандартнаяОбработка = Ложь;	
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция выбора периода отчета 
//
// Параметры:
//  Форма - форма, из которой вызвано событие
//  Элемент - элемент формы
//  СтандартнаяОбработка - флаг использования стандартной обработки
//  ВидПериода - вид периода
//  НачалоПериода - начало периода
//
// Возвращаемые значения:
//  ВыбранныйПериод  - определяет период по началу периода и виду периода
//
Функция ВыбратьПериодОтчета(Форма, Элемент, СтандартнаяОбработка, ВидПериода, НачалоПериода)
	
	Список = ВыборПериодаКлиентСервер.ПолучитьСписокПериодов(НачалоПериода, ВидПериода);
	Если Список.Количество() = 0 Тогда
		СтандартнаяОбработка = Ложь;
		Возврат Неопределено;
	КонецЕсли;
	
	ЭлементСписка = Список.НайтиПоЗначению(НачалоПериода);
	ВыбранныйПериод = Форма.ВыбратьИзСписка(Список, Элемент, ЭлементСписка);
	
	Если ВыбранныйПериод = Неопределено тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Индекс = Список.Индекс(ВыбранныйПериод);
	Если Индекс = 0 ИЛИ Индекс = Список.Количество() - 1 тогда
		ВыбранныйПериод = ВыбратьПериодОтчета(Форма, Элемент, СтандартнаяОбработка, ВидПериода, ВыбранныйПериод.Значение);
	КонецЕсли;
	
	Возврат ВыбранныйПериод;
	
КонецФункции