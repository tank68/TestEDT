///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2019, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// Подсистема "_ДемоИнтернетПоддержкаПользователей._ДемоРаботаСКлассификаторами".
// ОбщийМодуль._ДемоРаботаСКлассификаторамиКлиент.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Загружает файл классификатора из сервиса и сохраняет его в файл.
//
// Параметры:
//  Идентификатор - Строка - идентификатор классификатора в сервисе.
//
Процедура ПолучитьФайлыКлассификаторов(Идентификатор) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Идентификатор", Идентификатор);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьФайлыКлассификаторовПослеПодключения",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ФайловаяСистемаКлиент.ПодключитьРасширениеДляРаботыСФайлами(ОписаниеОповещения);
	
КонецПроцедуры

// Загружает файл классификатора из сервиса и сохраняет его в файл.
//
// Параметры:
//  РезультатПодключения - Булево - результат подключения;
//  ДополнительныеПараметры - Структура - доп. параметры подключения.
//
Процедура ПолучитьФайлыКлассификаторовПослеПодключения(РезультатПодключения, ДополнительныеПараметры) Экспорт
	
	Если РезультатПодключения <> Истина Тогда
		Возврат;
	КонецЕсли;
	
	Идентификаторы = Новый Массив;
	Идентификаторы.Добавить(ДополнительныеПараметры.Идентификатор);
	
	РезультатОперации = УА_РаботаСКлассификаторамиВызовСервера.ПолучитьФайлыКлассификаторов(Идентификаторы);
	
	Если ЗначениеЗаполнено(РезультатОперации.КодОшибки) Тогда
		Если РезультатОперации.ЭтоПолноправныйПользователь Тогда
			ПоказатьПредупреждение(, РезультатОперации.ИнформацияОбОшибке);
		Иначе
			ПоказатьПредупреждение(, РезультатОперации.СообщениеОбОшибке);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.ДанныеФайла = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Файл не загружен'"));
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("РезультатОперации", РезультатОперации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьФайлыКлассификаторовЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ДВФ = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(
		ОписаниеОповещения,
		ДВФ);
	
КонецПроцедуры

// Сохраняет полученный классификатор в файл.
//
// Параметры:
//  ВыбранныеФайлы - Массив - содержит путь к выбранному файлу;
//  ДополнительныеПараметры - Структура - доп. параметры сохранения.
//
Процедура ПолучитьФайлыКлассификаторовЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПолучитьФайлыКлассификаторовПослеСохранения",
		ЭтотОбъект);
	
	РезультатОперации = ДополнительныеПараметры.РезультатОперации;
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		РезультатОперации.ДанныеФайла.НачатьЗапись(
			ОписаниеОповещения,
			ВыбранныеФайлы[0]);
	КонецЕсли;
	
КонецПроцедуры

// Показывает оповещение об успешной загрузке.
//
// Параметры:
//  ДополнительныеПараметры - Структура - доп. параметры сохранения.
//
Процедура ПолучитьФайлыКлассификаторовПослеСохранения(ДополнительныеПараметры) Экспорт
	
	ПоказатьОповещениеПользователя(
		НСтр("ru = 'Файл классификатора успешно сохранен.'"),
		,
		НСтр("ru = 'Файл загружен на локальный диск или сетевой каталог.'"),
		БиблиотекаКартинок.Информация32);
	
КонецПроцедуры

#КонецОбласти