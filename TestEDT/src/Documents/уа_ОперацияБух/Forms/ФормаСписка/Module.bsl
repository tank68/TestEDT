#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	//Если ИмяСобытия = "ИзменениеОсновногоПортфеля" Тогда
	//	ОбщегоНазначенияДУКлиент.ИзменитьОтборПоОсновномуПортфель(Список, ,Параметр);
	//КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПортфельПриИзменении(Элемент)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(Список, "Портфель", 
		ОтборПортфель, ЗначениеЗаполнено(ОтборПортфель));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ПоказыватьПроводки Тогда
		ПодключитьОбработчикОжидания("ОбновитьПроводки", 0.1, Истина);
	КонецЕсли;

//	ДокументСсылка = Элемент.ТекущаяСтрока;
//	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(РегистрБухгалтерии.Отбор, "Регистратор",
//		ДокументСсылка, ВидСравненияКомпоновкиДанных.Равно, "Регистратор", Истина);	

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоказатьСкрытьПроводки(Команда)
	
	ПоказыватьПроводки = НЕ ПоказыватьПроводки;
	УправлениеФормой(ЭтаФорма);
	Если ПоказыватьПроводки Тогда
		ПодключитьОбработчикОжидания("ОбновитьПроводки", 0.1, Истина);
	Иначе 
		ОтключитьОбработчикОжидания("ОбновитьПроводки");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьАктивностьПроводок(Команда)
	
	ТекДокумент = ПолучитьДокумент();
	Если ТекДокумент <> Неопределено Тогда
		ПереключитьАктивностьПроводокСервер(ТекДокумент);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьДокумент()
	
	ТекДанные = Элементы.Список.ТекущиеДанные;
	
	Если ТекДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран документ'"));
		Возврат Неопределено;
	КонецЕсли;
	
	ТекДокумент = ТекДанные.Ссылка;
	Если НЕ ЗначениеЗаполнено(ТекДокумент) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Не выбран документ'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ТекДокумент;
	
КонецФункции

&НаСервере
Процедура ПереключитьАктивностьПроводокСервер(Документ)
	
	БухгалтерскийУчет.ПереключитьАктивностьПроводокБУ(Документ);
	Элементы.Проводки.Обновить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Форма.Элементы.ФормаПоказатьСкрытьПроводки.Пометка = Форма.ПоказыватьПроводки;
	
	Форма.Элементы.Проводки.Видимость = Форма.ПоказыватьПроводки;
	
	Форма.Высота = 20;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПроводки()

	Регистратор = ?(Элементы.Список.ТекущиеДанные = Неопределено, Неопределено, Элементы.Список.ТекущиеДанные.Ссылка);
	Проводки.Параметры.УстановитьЗначениеПараметра("Регистратор", Регистратор);

КонецПроцедуры

#КонецОбласти
