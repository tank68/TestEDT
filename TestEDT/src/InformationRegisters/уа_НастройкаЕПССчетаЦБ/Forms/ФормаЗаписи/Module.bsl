
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КатегорияАктиваПриИзменении(Элемент)
	ОбработатьИзменениеКатегорииЦБ();
КонецПроцедуры

#КонецОбласти


#Область СерверныеОбработчикиСобытийЭлементовФормы

&НаСервере
Процедура ОбработатьИзменениеКатегорииЦБ()
	
	УправлениеФормой();
	
	Если Элементы.КлассификацияЦБ.ПараметрыВыбора.Количество() > 0 И
		ВРег(Элементы.КлассификацияЦБ.ПараметрыВыбора[0].Имя) = ВРег("Отбор.Ссылка") Тогда
		
		Значение = Элементы.КлассификацияЦБ.ПараметрыВыбора[0].Значение;
		Если Значение.Найти(Запись.КлассификацияЦБ) = Неопределено Тогда
			Запись.КлассификацияЦБ = Неопределено;
		КонецЕсли;
	Иначе
		Запись.КлассификацияЦБ = Неопределено;
	КонецЕсли;
	
	Если Элементы.ОценочнаяКатегория.ПараметрыВыбора.Количество() > 0 И
		ВРег(Элементы.ОценочнаяКатегория.ПараметрыВыбора[0].Имя) = ВРег("Отбор.Ссылка") Тогда
		
		Значение = Элементы.ОценочнаяКатегория.ПараметрыВыбора[0].Значение;
		Если Значение.Найти(Запись.ОценочнаяКатегория) = Неопределено Тогда
			Запись.ОценочнаяКатегория = Неопределено;
		КонецЕсли;
	Иначе
		Запись.ОценочнаяКатегория = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область Прочее

&НаСервере
Процедура УправлениеФормой()
	
	// Отбор классификаций ЦБ
	ОтборПоСсылке = Новый ПараметрВыбора("Отбор.Ссылка", 
		Перечисления.уа_КлассификацияЦБ.КлассификацииПоКатегорииЦБ(Запись.КатегорияАктива));
	
	ПараметрыВыбораКлассификации = Новый Массив;
	ПараметрыВыбораКлассификации.Добавить(ОтборПоСсылке);
	
	Элементы.КлассификацияЦБ.ПараметрыВыбора = 
		Новый ФиксированныйМассив(ПараметрыВыбораКлассификации);
	
	// Отбор оценочных категорий
	ОтборПоСсылке = Новый ПараметрВыбора("Отбор.Ссылка", 
		Перечисления.уа_ОценочнаяКатегория.ОценочныеКатегорииПоКатегорииЦБ(Запись.КатегорияАктива));
	
	ПараметрыВыбораОценочнойКатегории = Новый Массив;
	ПараметрыВыбораОценочнойКатегории.Добавить(ОтборПоСсылке);
	
	Элементы.ОценочнаяКатегория.ПараметрыВыбора = 
		Новый ФиксированныйМассив(ПараметрыВыбораОценочнойКатегории);
	
КонецПроцедуры


#КонецОбласти

