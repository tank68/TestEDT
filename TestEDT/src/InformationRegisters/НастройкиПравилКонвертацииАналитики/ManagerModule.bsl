#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПРОГРАМНЫЙ_ИНТЕРФЕЙС 

Функция ПравилаКонвертацииАналитики(ТипИсточника, Портфель, Дата) Экспорт 

	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаПравилаКонвертацииАналитики();
		
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("Портфель", Портфель);
	Запрос.УстановитьПараметр("ПустойПортфель", Справочники.Портфели.ПустаяСсылка());
	Запрос.УстановитьПараметр("Источник", ТипИсточника);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Результат = Новый Массив;
	Пока Выборка.Следующий() Цикл
		СтруктураПравила = Новый Структура("Ссылка, Источник, Приемник, ПроизвольныйАлгоритм, РеквизитИсточника, ТекстАлгоритма");
		ЗаполнитьЗначенияСвойств(СтруктураПравила, Выборка);
		Результат.Добавить(СтруктураПравила);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции // ПравилаКонвертацииАналитики()

#КонецОбласти

#Область ВСПОМОГАТЕЛЬНЫЕ_ПРОЦЕДУРЫ_ФУНКЦИИ

Функция ТекстЗапросаПравилаКонвертацииАналитики()
	
	Возврат 	
		"ВЫБРАТЬ
		|	ПравилаКонвертацииАналитикСрезПоследних.НаборПравилКонвертации КАК НаборПравилКонвертации
		|ПОМЕСТИТЬ НаборПравилПоПортфелю
		|ИЗ
		|	РегистрСведений.НастройкиПравилКонвертацииАналитики.СрезПоследних(&Дата, Портфель = &Портфель) КАК ПравилаКонвертацииАналитикСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПравилаКонвертацииАналитикСрезПоследних.НаборПравилКонвертации КАК НаборПравилКонвертации
		|ПОМЕСТИТЬ НаборПравилПоПустомуПортфелю
		|ИЗ
		|	РегистрСведений.НастройкиПравилКонвертацииАналитики.СрезПоследних(&Дата, Портфель = &ПустойПортфель) КАК ПравилаКонвертацииАналитикСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПравилКонвертацииПравилаКонвертации.ПравилоКонвертацииАналитики.Ссылка КАК ПравилоКонвертацииЗаполненногоПортфеля,
		|	НаборПравилКонвертацииПравилаКонвертации.ПравилоКонвертацииАналитики.Приемник КАК Приемник
		|ПОМЕСТИТЬ ПравилаПоПортфелю
		|ИЗ
		|	Справочник.НаборПравилКонвертации.ПравилаКонвертации КАК НаборПравилКонвертацииПравилаКонвертации
		|ГДЕ
		|	НаборПравилКонвертацииПравилаКонвертации.Ссылка В
		|			(ВЫБРАТЬ
		|				НаборПравилПоПортфелю.НаборПравилКонвертации
		|			ИЗ
		|				НаборПравилПоПортфелю КАК НаборПравилПоПортфелю)
		|	И НаборПравилКонвертацииПравилаКонвертации.ПравилоКонвертацииАналитики.Источник = &Источник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	НаборПравилКонвертацииПравилаКонвертации.ПравилоКонвертацииАналитики.Ссылка КАК ПравилоКонвертацииПустогоПортфеля,
		|	НаборПравилКонвертацииПравилаКонвертации.ПравилоКонвертацииАналитики.Приемник КАК Приемник
		|ПОМЕСТИТЬ ПравилаПоПустомуПортфелю
		|ИЗ
		|	Справочник.НаборПравилКонвертации.ПравилаКонвертации КАК НаборПравилКонвертацииПравилаКонвертации
		|ГДЕ
		|	НаборПравилКонвертацииПравилаКонвертации.Ссылка В
		|			(ВЫБРАТЬ
		|				НаборПравилПоПустомуПортфелю.НаборПравилКонвертации
		|			ИЗ
		|				НаборПравилПоПустомуПортфелю КАК НаборПравилПоПустомуПортфелю)
		|	И НаборПравилКонвертацииПравилаКонвертации.ПравилоКонвертацииАналитики.Источник = &Источник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПравилаПоПортфелю.Приемник КАК Приемник,
		|	ПравилаПоПортфелю.ПравилоКонвертацииЗаполненногоПортфеля КАК ПравилоКонвертацииЗаполненногоПортфеля,
		|	ПравилаПоПустомуПортфелю.ПравилоКонвертацииПустогоПортфеля КАК ПравилоКонвертацииПустогоПортфеля
		|ПОМЕСТИТЬ ПравилаОбщие
		|ИЗ
		|	ПравилаПоПортфелю КАК ПравилаПоПортфелю
		|		ПОЛНОЕ СОЕДИНЕНИЕ ПравилаПоПустомуПортфелю КАК ПравилаПоПустомуПортфелю
		|		ПО ПравилаПоПортфелю.Приемник = ПравилаПоПустомуПортфелю.Приемник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ПравилаОбщие.ПравилоКонвертацииЗаполненногоПортфеля ЕСТЬ NULL
		|			ТОГДА ПравилаОбщие.ПравилоКонвертацииПустогоПортфеля
		|		ИНАЧЕ ПравилаОбщие.ПравилоКонвертацииЗаполненногоПортфеля
		|	КОНЕЦ КАК Правило
		|ПОМЕСТИТЬ ВТ_Правила
		|ИЗ
		|	ПравилаОбщие КАК ПравилаОбщие
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Правила.Правило КАК Ссылка,
		|	ВТ_Правила.Правило.Источник КАК Источник,
		|	ВТ_Правила.Правило.Приемник КАК Приемник,
		|	ВТ_Правила.Правило.ПроизвольныйАлгоритм КАК ПроизвольныйАлгоритм,
		|	ВТ_Правила.Правило.РеквизитИсточника КАК РеквизитИсточника,
		|	ВТ_Правила.Правило.ТекстАлгоритма КАК ТекстАлгоритма
		|ИЗ
		|	ВТ_Правила КАК ВТ_Правила";
		
КонецФункции

#КонецОбласти

#КонецЕсли